# 2 数据类型

本章详细介绍Metal数据类型，包括代表向量(vectors)和矩阵(matrices)的类型。还讨论了原子数据类型、缓冲区(buffers)、纹理、采样器、数组和用户自定义的结构。还描述了类型对齐(Type alignment)和类型转换。

## 2.1 标量数据类型
Metal 支持`表1`中罗列的标量类型，Metal 不支持 `double`, `long`, `unsigned long`, `long long`, `unsigned longlong`和 `long double` 数据类型。

`表1 - Metal 标量数据类型`

| 类型 | 描述 |
| --- | --- |
| bool | 条件数据类型，其值为true或falsee。 值 true 扩展为整数常量1，值 false 扩展为整数常量0。 |
| char <br> int8_t | 带符号的二进制补码8位整数。 |
| unsigned char <br> uchar <br> uint8_t  |  无符号的8位整数 |
| short <br> int16_t| 带符号的二进制补码16位整数 |
| unsigned short <br> ushort <br> uint16_t | 无符号的16位整数 |
| int <br> int32_t | 带符号的二进制补码32位整数 |
| unsigned int <br> uint <br> uint32_t | 无符号的32位整数 |
| half | 一个16位浮点。 半数据类型必须符合IEEE 754 二进制16 存储格式 |
| float | 一个32位浮点。浮点数据类型必须符合IEEE 754 单精度存储格式 |
| size_t| `sizeof`运算符的结果的无符号整数类型。这是一个64无符号整数 | 
| ptrdiff_t | 有符号整数类型，它是两个指针相减的结果。这是一个64带符号整数 |
| void | void类型包含一组空值;它是一种不完整的类型  |

> 注意: Metal 支持标准的 `f`或`F`后缀，以指定单精度浮点字面值(例如，0.5f或0.5F)。此外，Metal支持`h`或`H`后缀以指定半精度浮点字面值（例如，0.5h或0.5H）。Metal还支持无符号整数文字的`u`或`U`后缀。

表2列出了大多数标量数据类型的大小和对齐方式。

`表2 - 标量数据类型的大小和对齐`

| 类型 | 大小(以字节计) |  对齐方式(以字节计) |
| --- | --- | --- |
| bool | 1 | 1 |
| char <br> int8_t <br> unsigned char <br> uchar <br> uint8_t  | 1 | 1 |
| short <br> int16_t <br> unsigned short <br> ushort <br> uint16_t | 2 | 2 | 
| int <br> int32_t <br> unsigned int <br> uint <br> uint32_t | 4 | 4 |
| half | 2 | 2 |
| float | 4 | 4 |


## 2.2 矢量和矩阵

Metal支持由系统向量数学课实现的向量数据类型的子集。

支持的向量类型名称是：
`booln`、`charn`、`shortn`、`intn`、`ucharn`、`ushort`、`uintn`、`halfn`和`floatn`。
其中`n`是2、3或4，表示2-、3-、4-组向量类型。表3列出了向量数据类型的大小和对齐方式。

`表3 - 向量数据类型的大小和对齐`

| 类型 | 大小(以字节计) |  对齐方式(以字节计) |
| --- | --- | --- |
| bool2 | 2 | 2 |
| bool3 | 4 | 4 |
| bool4 | 4 | 4 |
| char2 <br> uchar2 | 2 | 2 | 
| char3 <br> uchar3 | 4 | 4 | 
| char4 <br> uchar4 | 4 | 4 | 
| short2 <br> ushort2 | 4 | 4 |
| short3 <br> ushort3 | 8 | 8 |
| short4 <br> ushort4 | 8 | 8 |
| int2 <br> uint2 | 8 | 8 |
| int3 <br> uint3 | 16 | 16 |
| int4 <br> uint4 | 16 | 16 |
| half2 | 4 | 4 |
| half3 | 8 | 8 |
| half4 | 8 | 8 |
| float2 | 8 | 8 |
| float3 | 16 | 16 |
| float4 | 16 | 16 |

### 2.2.1 访问向量组件

可以使用数组索引访问向量组件。数组索引0表示向量的第一个组件，索引1表示第二个组件，以此类推。以下示例显示了访问阵列组件的各种方法：

```metal
pos = float4(1.0f, 2.0f, 3.0f, 4.0f);

float x = pos[0]; // x = 1.0
float x = pos[2]; // z = 3.0

float4 vA = float4(1.0f, 2.0f, 3.0f, 4.0f);
float3 vB;

for (int i=0; i<4; i++)
    vB[i] = vA[i] * 2.0f // vB = (2.0, 4.0, 6.0, 8.0); 
```

Metal 支持使用点(.)作为选择运算符来访问向量组件，使用可以指示坐标或者颜色数据的字母：

```metal
<vector_data_type>.xyzw
<vector_data_type>.rgba
```

在以下的代码中，初始化向量test，然后使用.xyzw或.rgba选择语法访问组件：

```metal
int4 test = int4(0, 1, 2, 3);
int a = test.x; // a = 0
int b = test.y; // b = 1
int c = test.z; // c = 2
int d = test.w; // d = 3
int e = test.r; // e = 0
int f = test.g; // f = 1
int g = test.b; // g = 2 
int h = test.a; // h = 3 
```

组件选择语法允许选择多个组件。

```metal
float4 c;
c.xyzw = float4(1.0f, 2.0f, 3.0f, 4.0f);
c.z = 1.0f;
c.xy = float2(3.0f, 4.0f);
c.xyz = float3(3.0f, 4.0f, 5.0f);
```

组件选择语法还允许组件被置换或复制。

```metal
float4 pos = float4(1.0f, 2.0f, 3.0f, 4.0f);
float4 swiz = pos.wzyx; // swiz = (4.0f, 3.0f, 2.0f, 1.0f)
float4 dup = pos.xxyy;  // dup = (1.0f, 1.0f, 2.0f, 2.0f)
```

组件组表示可以出现在表达式的左侧。为了形成左值，可以应用混合。得到的左值可以是标量或向量类型，具体取决于指定的组件数量。生成的向量类型的左值不得包含重复的组件。

```metal
float4 pos = float4(1.0f, 2.0f, 3.0f, 4.0f);
// pos = (5.0, 2.0, 3.0, 6.0)
pos.xw = float2(5.0f, 6.0f);
// pos = (8.0, 2.0, 3.0, 7.0)
pos.wx = float2(7.0f, 8.0f);
// pos = (3.0, 5.0, 9.0, 7.0)
pos.xyz = float3(3.0f, 5.0f, 9.0f);
```

不允许使用以下的向量组件访问方法，会导致编译时错误：

* 访问超出向量类型声明的组件是错误的。2-组件向量数据类型只能访问.xy或.rg元素。3-组件向量数据类型只能访问 .xyz或.rgb元素。

    ```metal
    float2 pos;
    pos.x = 1.0f; // is legal; so is y
    pos.z = 1.0f; // is illegal; so is w
    float3 pos;
    pos.z = 1.0f; // is legal
    pos.w = 1.0f; // is illegal
    ```

* 在左侧访问相同的组件两次是不明确的，是一个错误。

    ```metal
     // illegal - 'x' used twice
     pos.xx = float2(3.0f, 4.0f);
    ```

* 访问不同数量的组件是错误的。

    ```metal
    // illegal - mismatch between float2 and float4
    pos.xy = float4(1.0f, 2.0f, 3.0f, 4.0f);
    ```

* 在单次访问中混合.rgba和.xyzw语法是错误的。

    ```metal
    float4 pos = float4(1.0f, 2.0f, 3.0f, 4.0f);
    pos.x = 1.0f; // OK
    pos.g = 2.0f; // OK
    pos.xg = float2(3.0f, 4.0f); // illegal - mixed attributes used
    float3 coord = pos.ryz;  // illegal - mixed attributes used
    ```

* 带有 swizzles的向量的指针或引用是错误的。

    ```metal
    float4 pos = float4(1.0f, 2.0f, 3.0f, 4.0f);
    my_func(&pos.xy); // illegal
    ```

向量类型的`sizeof`运算符返回向量的大小，该大小以组件数*每个组件的大小给出。例如，`sizeof(float4)`返回16，`sizeof(half4)`返回8。

### 2.2.2 矢量构造函数

构造函数可用于从一组标量或向量创建向量。初始化向量时，其参数签名确定其构造方式。例如，如果仅使用单个标量初始化向量，则构造向量的所有组件都将设置为该标量值。

如果向量是由多个标量，一个或多个向量或这些向量的混合构成的，则从参数的组成部分开始按顺序构造向量的组件。参数从左到右消耗。在消耗下一个参数的任何组件之前，每个参数都按顺序使用其所有组件。

这是一个可用于`float4`的构造函数的完整列表：

```metal
float4(float x);
float4(float x, float y, float z, float w);
float4(float2 a, float2, b);
float4(float a, float b, float2 c);
float4(float a, float2 b, float c);
float4(float3 a, float b);
float4(float a, float3 b);
float4(float4 x);
```

这是一个可用于`float3`的构造函数的完整列表：

```metal
float3(float x); 
float3(float x, float y, float z);  
float3(float a, float2 b);  
float3(float2 a, float b);  
float3(float3 x);
```

这是一个可用于`float2`的构造函数的完整列表：

```metal
float2(float x); 
float2(float x, float y);  
float2(float2 x);
```

以下示例说明了构造函数的用法：

```metal
float x = 1.0f, y = 2.0f, z = 3.0f, w = 4.0f; 
float4 a = float4(0.0f); 
float4 b = float4(x, y, z, w); 
float2 c = float2(5.0f, 6.0f);

float2 a = float2(x, y);
float2 b = float2(z, w);
float4 x = float4(a.xy, b.xy);
```

初始化向量构造函数是一个编译时错误。


### 2.2.3 打包向量类型

2.2节中描述的向量数据类型和向量大小对齐方式。开发人员还可以要求他们的向量数据紧密包装。例如，顶点结构可以包含紧密包装的位置、法线、切线向量和纹理坐标，并作为缓冲区传递给顶点函数。

支持的打包向量类型名称是：
`packed_charn`、`packed_shortn`、`packed_intn`、`packed_ucharn`、`packed_ushortn`、`packed_uintn`、`packed_halfn`和`packed_floatn`。
其中`n`是2、3或4，分别代表2-、3-或4-组分向量类型。（`packed_booln`向量类型名称是保留的。）

表4列出了打包向量数据类型的大小和对齐方式。

| 类型 | 大小(以字节计) |  对齐方式(以字节计) |
| --- | --- | --- |
| packed_char2, <br> packed_uchar2 | 2 | 1 |
| packed_char3, <br> packed_uchar3 | 3 | 1 |
| packed_char4, <br> packed_uchar4 | 4 | 1 |
| packed_short2, <br> packed_ushort2 | 4 | 2 |
| packed_short3, <br> packed_ushort3 | 6 | 2 |
| packed_short4, <br> packed_ushort4 | 8 | 2 |
| packed_int2, <br> packed_uint2 | 8 | 4 |
| packed_int3, <br> packed_uint3 | 12 | 4 |
| packed_int4, <br> packed_uint4 | 16 | 4 |
| packed_half2 | 4 | 2 |
| packed_half3 | 6 | 2 |
| packed_half4 | 8 | 2 |
| packed_float2 | 8 | 4 |
| packed_float3 | 12 | 4 |
| packed_float4 | 16 | 4 |

打包的向量数据类型通常用作数据存储格式。从打包的向量数据类型加载和存储到对齐的向量数据类型，反之亦然。支持复制构造函数和赋值运算符。打包向量数据类型也支持算术、逻辑和关系运算符。

例如：

```metal
device float4 *buffer;
device packed_float4 *packed_buffer;
int i;
packed_float4 f ( buffer[i] );
pack_buffer[i] = buffer[i];
// operator to convert from packed_float4 to float4
buffer[i] = float4( packed_buffer[i] );
```

可以使用数组索引访问打包向量数据类型的组件。但是无法使用`.xyzw`或`.rgba`选择语法访问打包向量数据类型的组件。

例如：

```metal
packed_float4 f;
f[0] = 1.0f;  // OK
f.x = 1.0f;   // Illegal - compilation error
```

## 2.3 矩阵数据类型

Metal支持由系统数学库实现的矩阵数据类型的子集。支持的矩阵类型名称是：
`halfnxm` 和 `floatnxm`
其中n和m是列数和行数。n和m必须是2、3或4。`floatnxm`类型的矩阵由n个`floatm`向量组成。类似的，`halfnxm`类型的矩阵由n个半向量组成。

表5列出了矩阵数据类型的大小和对齐方式。

| 类型 | 大小(以字节计) |  对齐方式(以字节计) |
| --- | --- | --- |
| half2x2 | 8 | 4 |
| half2x3 | 16 | 8 |
| half2x4 | 16 | 8 |
| half3x2 | 12 | 4 |
| half3x3 | 24 | 8 |
| half3x4 | 24 | 8 |
| half4x2 | 16 | 4 |
| half4x3 | 32 | 8 |
| half4x4 | 32 | 8 |
| float2x2 | 16 | 8 |
| float2x3 | 32 | 16 |
| float2x4 | 32 | 16 |
| float3x2 | 24 | 8 |
| float3x3 | 48 | 16 |
| float3x4 | 48 | 16 |
| float4x2 | 32 | 8 |
| float4x3 | 64 | 16 |
| float4x4 | 64 | 16 |

### 2.3.1 访问矩阵组件

可以使用数组下标语法访问矩阵的组件。将单个下表应用于矩阵将矩阵视为列向量的数组。两个下标选择一列，然后选择一行。顶部列是第0列，然后第2个下标对结果向量进行操作，如前面对向量所定义的那样。

```metal
float4x4 m;
// sets the 2nd column to all 2.0
m[1] = float4(2.0f);
// sets the 1st element of the 1st column to 1.0
m[0][0] = 1.0f;
// sets the 4th element of the 3rd column to 3.0
m[2][3] = 3.0f;
```

`floatnxm`和`halfnxm`矩阵可以作为n个`floatm`或n个`halfm`条目的数组进行访问。

使用非常量表达式访问矩阵边界之外的组件会导致未定义的行为。使用常量表达式访问矩阵边界之外的矩阵组件会导致编译时错误。

### 2.3.2 矩阵构造函数

构造函数可用于从一组标量、向量或者矩阵创建矩阵。初始化矩阵时，其参数签名决定了它的构造函数。例如，如果仅使用单个标量初始化矩阵，则结果是包含矩阵对角线的所有分量的标量的矩阵，其余分量初始化为0.0。例如调用
 `float4x4(fval);`
其中`fval`是标量浮点值，用这些初始内容构造一个矩阵：

```metal 
fval 0.0 0.0 0.0
0.0 fval 0.0 0.0
0.0 0.0 fval 0.0
0.0 0.0 0.0 fval
```

矩阵也可以由具有相同大小的另一个矩阵构成；即，具有相同数量的行和列。例如，

```metal
float3x4(float3x4);
float3x4(half3x4);
```

矩阵组件按列主要顺序构建和使用。矩阵构造函数必须在其参数中指定足够的值，以初始化构造的矩阵对象中的每个组件。提供多余必要的参数会导致错误。初始化矩阵构造函数会导致编译时错误。

具有n列和m行的类型T的矩阵也可以由具有m个分量的类型T的n个向量构成。以下示例是合法的构造函数：

```metal
float2x2(float2, float2);
float3x3(float3, float3, float3);
float3x2(float2, float2, float2);
```

从Metal v2.0开始，具有n列和m行的类型T的矩阵也可以由类型为T的n*m个标量构成。以下示例是合法的构造函数：

```metal
float2x2(float, float, float, float);
float3x2(float, float, float, float, float, float);
```

以下是不受支持的矩阵构造函数的示例。不能从向量和标量的组合构造矩阵。

```metal
// not supported
float2x3(float2 a, float b, float2 c, float d);
```

## 2.4 数据类型对齐方式

`alignas`对齐说明符可用于指定类型或对象的对齐要求。`alignas`说明符可以应用于变量的声明结构或类的数据成员。它也可以应用于struct、class、或 enumeration 类型的声明。

Metal编译器负责根据数据类型的要求将数据项对齐到适当的对齐方式。对于声明为数据类型指针的图形或内核函数的参数，Metal编译器可以假定指针对象始终根据数据类型的要求进行适当的对齐。

## 2.5 原子数据类型

原子类型的对象是唯一没有数据竞争的Metal着色语言对象。如果一个线程写入原子对象而另一个线程从中读取，则行为是明确定义的。

支持如下这些原子类型：

* `atomic_int` 所有的系统，Metal 1.0 以上版本
* `atomic_uint` 所有的系统，Metal 1.0 以上版本
* `atomic_bool` iOS系统 Metal 2.0 以上版本；macOS 不支持
* `atomic<T>`  iOS系统 Metal 2.0 以上版本；macOS 不支持

`atomic<T>` 表示模板化类型，其中T可以是`int`、`uint`或`bool`。

Metal原子数据类型仅限于Metal原子功能使用，如第5.13节所述。这些原子函数是C++ 14原子和同步函数的子集。

## 2.6 像素数据类型（Pixel Data Types）

> 所有操作系统：自v2.0后支持像素数据类型。

Metal像素数据类型是模板化类型，其描述像素格式类型及其对应的ALU类型。ALU类型表示加载操作返回的类型以及为存储操作指定的输入类型。像素数据类型通常在所有地址空间中可用（有关地址空间的详细信息，请参阅第4.2节）。

表6列出了Metal着色语言中支持的像素数据类型，以及它们的大小和对齐方式。

| 像素数据类型 | T的可选类型 | 大小（字节） | 大小（字节） |
| ---  | --- | --- |  --- |
| r8unorm<T> | half 或 float | 1 | 1 |
| r8snorm<T> | half 或 float | 1 | 1 |
| r16unorm<T> | float | 2 | 2 |
| r16snorm<T> | float | 2 | 2 |
| rg8unorm<T> | half2 或 float2 | 2 | 1 |
| rg8snorm<T> | half2 或 float2 | 2 | 1 |
| rg16unorm<T> | float2 | 4 | 2 |
| rg16snorm<T> | float2 | 4 | 2 |
| rgba8unorm<T> | half4 或 float4 | 4 | 1 |
| srgba8unorm<T> | half4 或 float4 | 4 | 1 |
| rgba8snorm<T> | half4 或 float4 | 4 | 1 |
| rgba16unorm<T> | float4 | 8 | 2 |
| rgba16snorm<T> | float4 | 8 | 2 |
| rgb10a2<T> | half4 或 float4 | 4 | 4 |
| rg11b10f<T> | half3 或 float3 | 4 | 4 |
| rgb9e5<T> | half3 或 float3 | 4 | 4 |

仅允许像素数据类型与其对应的ALU类型之间的分配和等式/不等式比较。（下面的代码中出现的 `buffer(n)`属性将在4.3.1节中解释）

例子：

```metal
kernel void
my_kernel(device rgba8unorm<half4> *p [[buffer(0)]],
          uint gid [[thread_position_in_grid]], …)
{
    rgba8unorm<half4> x = p[index]; half4 val = p[gid];
    …
    p[gid] = val;
    p[index] = x; 
}
```

例子：

```metal
struct Foo {
    rgba8unorm<half4> a;
}; 

kernel void 
my_kernel(device Foo *p [[buffer(0)]], 
        uint gid [[thread_position_in_grid]], …) 
{
    half4 a = p[gid].a;
    …
    p[gid].a = a; 
}

```

## 2.7 缓冲区(Buffers)

Metal 将缓存区实现为指向 `device`、`threadgroup`或`constant`地址空间中描述的内置或者用户定义数据类型的指针（有关这些地址属性的完成说明，请参阅第4.2节）。这些缓存区可以在程序范围内声明，也可以作为参数传递给函数。

例如：

```metal
device float4 *device_buffer;

struct my_user_data {
    float4 a;
    float b;
    int2 c; 
};

constant my_user_data *user_data;
```

普通Metal缓冲区可以包含：

* 基本类型，如`float`和`int`
* 向量和矩阵类型
* 缓冲区类型的数组
* 缓冲区类型的结构
* 缓冲区类型的联合

有关把缓冲区作为参数，请参阅第2.12节。

## 2.8 纹理(Texture)

纹理数据类型是一维、二维、三维纹理数据的句柄，其对应纹理的单个 mipmap级别的全部或一部分。以下模板定义特定的纹理数据类型：

```metal
enum class access { sample, read, write, read_write };
texture1d<T, access a = access::sample> 
texture1d_array<T, access a = access::sample> 
texture2d<T, access a = access::sample> 
texture2d_array<T, access a = access::sample> 
texture3d<T, access a = access::sample> 
texturecube<T, access a = access::sample> 
texturecube_array<T, access a = access::sample> 
texture2d_ms<T, access a = access::read> 
```

必须将具有深度格式的纹理声明为以下纹理数据类型之一：

```metal
depth2d<T, access a = access::sample>
depth2d_array<T, access a = access::sample>
depthcube<T, access a = access::sample>
depthcube_array<T, access a = access::sample>
depth2d_ms<T, access a = access::read> 
```

`T`指定了从纹理中读取的 或者写入到纹理中的单组颜色类型， 对于除了深度纹理类型的其他纹理类型，T可以是`half`、`float`、`short`、`ushort`、`int` 或者 `uint`。 深度纹理类型，`T`必须是`float`。

> 注意：如果`T`是 `int`或者`short`，与纹理关联的数据必须使用带符号的整型格式。如果`T`是`uint`或者是`ushort`，与纹理关联的数据必须使用无符号的整型格式。如果`T`是`half`，与纹理关联的数据必须使用归一化(normalized)的（带符号或者无符号的整型）或者半精度的格式。如果`T`是`float`，与纹理关联的数据必须使用归一化(normalized)的（带符号或者无符号的整型）或者单精度的格式。

`access`属性描述了纹理的访问权限，如下列举了支持的属性。

* `sample` - 纹理对象可以被采样。`sample`意味着可以不使用采样器直接读取纹理的能力。
* `read` - 显卡或者内核函数只能从纹理读取数据。
* `write` - 显卡或者内核函数可以写入数据到纹理对象中。
* `read_write` - 显卡或者内核函数可以读写纹理对象。

所有的系统: `read_write` 访问权限从Metal 1.2 开始支持。而其他的访问权限从Metal 1.0就开始支持。

注意：对于多重采样纹理，只能使用`read`修饰符。对于深度纹理，只能使用 `sample`和`read`修饰符。

如下的例子

```metal
void foo (texture2d<float> imgA [[texture(0)]],
 texture2d<float, access::read> imgB [[texture(1)]],
texture2d<float, access::write> imgC [[texture(2)]])
{…} 
```
(See section 4.3.1 for a description of the texture attribute.) 

纹理类型可以用来作为函数内任何申明的变量类型。定义在函数内部的纹理类型的权限修饰符只能使用`acess::read`或者 `access::sample`。在函数方法体中如果不用`acess::read`或`access::sample`修饰符来定义纹理类型会报编译错误。

例子：

```metal
void foo (texture2d<float> imgA [[ texture(0) ]], 
          texture2d<float, access::read> imgB [[ texture(1) ]],
          texture2d<float, access::write> imgC [[ texture(2) ]])
{
    texture2d<float> x = imgA; // legal
    texture2d<float, access::read> y = imgB; // legal
    texture2d<float, access::write> z; // illegal 
    ...
}
```

### 2.8.1 纹理Buffers

所有的系统：纹理Buffers 在Metal2.1开始支持。




## 2.9 采样器

`sampler`类型标识了如何对纹理进行采样。Metal API允许您创建一个采样器对象，并将其作为参数传给图形或者内核函数。采样器对象也可以在程序源中而不是在API中描述。对于这些情况，我们只允许指定采样器状态的子集：寻址模式、滤波器模式、归一化坐标系和比较函数。

表7描述了支持的采样器状态枚举以及关联（和默认值）的列表。在Metal程序源中初始化采样器时，可以指定这些状态。

| 枚举名称 | 可取值 | 描述 |
| ---  | --- | --- |
| coord | normalized(默认值) <br> pixel | 1 |
| address | repeat <br> mirrored_repeat <br> clamp_to_edge(默认值) <br> clamp_to_zero <br> clamp_to_border | xxx |
| s_address <br> t_address <br> r_address | repeat <br> mirrored_repeat <br> clamp_to_edge(默认值) <br> clamp_to_zero <br> clamp_to_border | 2 |
| border_color | transparent_black (默认值) <br> opaque_black <br> opaque_white | 4 |
| filter | nearest(默认值) <br> linear | 2 |
| mag_filter | nearest（默认值） <br> linear  | 设置纹理采样时的放大滤波模式 |
| min_filter | nearest（默认值） <br> linear | 设置纹理采样时的缩小滤波模式 |
| mip_filter | none(默认值) <br> nearest <br> linear | 设置纹理采样时的mipmap过滤模式。如果为`none`,那么 |
| compare_func | never（默认值）<br> less <br> less_equal <br> greater <br> greater_equal <br> equal <br> not_equal <br> always | xx |

> macOS: `clamp_to_border`地址模式和`border_color`从v1.2开始支持。
> iOS: 不支持 `clamp_to_border` 地址模式和`border_color`。

使用 `clamp_to_border`，纹理外部的采样仅使用纹理坐标的边框颜色（并且不使用纹理边缘的任何颜色）。如果地址模式为 `clamp_to_border`，则`border_color`有效。

`clamp_to_zero`相当于`clamp_to_border`，边框颜色为 `transparent_black` (0.0, 0.0, 0.0)，有纹理alpha分量值。如果将`clamp_to_zero`指定为一个或多个纹理坐标的地址模式，则当且仅当边框颜色为`transparent_black`时，其他纹理坐标可以使用`clamp_to_border`的地址模式。否则行为未定义。

如果`coord`设置为`pixel`，则`min_filter`和`mag_filter`值必须相同，`mip_filter`值必须为`none`，地址模式必须为`clamp_to_zero`、`clamp_to_border`或`clamp_to_edge`。

除了枚举类型之外，还可以使用采样器指定以下类型：

```metal
max_anisotropy(int value)
lod_clamp(float min, float max)
```


以下Metal程序源说明了几种声明采样器的方法。（以下代码中出现的 `sampler(n)`属性在4.3.1节中进行了解释。）请注意，在程序源中声明的采样器或常量缓冲区不需要这些属性限定符。在Metal着色语言源中初始化的采样器使用`constexpr`声明。

```metal
constexpr sampler s(coord::pixel, 
                    address::clamp_to_zero, 
                    filter::linear);

constexpr sampler a(coord::normalized);

constexpr sampler b(address::repeat);

constexpr sampler s(address::clamp_to_zero, 
                    filter::linear, 
                    compare_func::less);

constexpr sampler s(address::clamp_to_zero, 
                    filter::linear, 
                    compare_func::less, 
                    max_anisotropy(10), 
                    lod_clamp(0.0f, MAXFLOAT));

kernel void
my_kernel(device float4 *p [[buffer(0)]],
        texture2d<float> img [[texture(0)]],
        sampler smp [[sampler(3)]],
        …)
{
        …
} 
```


## 2.10 图像块(Imageblocks)

> iOS: imageblock Metal 2.0 以上支持
> macOS： 不支持 imageblocks

图像块(Imageblocks)是在线程存储器中分配的二维数据结构（由宽度、高度和样本数表示），被设计为用于处理二维图像数据的有效机制。图像块的数据布局是不透明的。可以使用(x, y)坐标和可选的样本索引来访问图像块中的元素。与特别的 (x, y)相关联的图像块中的元素被称为每线程图像块数据或仅称为图像块数据。

图像块仅用于片段(fragment)和内核函数。



| 像素存储类型 | 兼容的纹理格式 |
| --- | --- |
| `float`, `half`| R32Float, R16Float, A8Unorm, R8Unorm, R8Snorm, R16Unorm, R16Snorm |
| float2, half2 | RG32Float, RG16Float, RG8Unorm, RG8Snorm, RG16Unorm, RG16Snorm |
| float4, half4 | RGBA32Float, RGBA16Float, RGBA8Unorm, RGBA8Snorm, RGBA16Unorm, RGBA16Snorm, RGB10A2Unorm, RG11B10Float, RGB9E5Float |
| int, short | R32Sint, R16Sint, R8Sint |
| int2, short2 | RG32Sint, RG16Sint, RG8Sint |
| int4, short4 | RGBA32Sint, RGBA16Sint, RGBA8Sint |
| uint, ushort | R32Uint, R16Uint, R8Uint |
| uint2, ushort2 | RG32Uint, RG16Uint, RG8Uint |
| uint4, ushort4 | RGBA32Uint, RGBA16Uint, RGBA8Uint |
| r8unorm<T> | A8Unorm, R8Unorm |
| r8snorm<T> | R8Snorm |
| r16unorm<T> | R16Unorm |
| r16snorm<T> | R16Snorm |
| rg8unorm<T> | RG8Unorm |
| rg8snorm<T> | RG8Snorm |
| rg16unorm<T> | RG16Unorm |
| rg16snorm<T> | RG16Snorm |
| rgba8unorm<T> | RGBA8Unorm, BGRA8Unorm |
| srgba8unorm<T> | RGBA8Unorm_sRGB, BGRA8Unorm_sRGB |
| rgba8snorm<T> | RGBA8Snorm, BGRA8Unorm |
| rgba16unorm<T> | RGBA16Unorm |
| rgba16snorm<T> | RGBA16Snorm |
| rgb10a2<T> | RGB10A2Unorm |
| rg11b10f<T> | RG11B10Float |
| rgb9e5<T> | RGB9E5Float |

第 2.10.1节 和 2.10.2 节分别描述了如何在片段和内核函数访问图像块。

### 2.10.1 片段函数中的图像块

在片段函数中，可以通过两种方式访问图像块：

* 作为颜色附件，其中图像块的存储布局在片段函数（隐式图像块布局）中是未知的。隐式图像块布局使用现有的颜色附件属性。见 2.10.1.1节
* 或者当在片段函数（显式图像块布局）中明确指定图像块的存储布局时，用作声明图像块数据的结构。见 2.10.1.2节。

#### 2.10.1.1 片段函数的隐式图像块布局

可以在片段函数中访问图像块数据（即，与像素相关联的图像块中的所有数据成员）。Metal创建一个隐式图像块，它匹配颜色附件（用于输入和从片段函数输出）的行为。在该模式中，与片段函数中描述的颜色附件相关联的类型是ALU类型（即用于在片段函数中执行计算的类型）。Metal运行时定义要使用的实际存储(即像素)格式。

当作为颜色附件访问图像块数据中，无法在图像块切片结构中声明2.6节描述的像素存储类型。

对于类型`T`类型的图像块数据隐式布局，`T`是一个结构，其中每个成员满足以下之一：

* 有颜色附件(参见4.3.4节 表12 中的 `[[color(m)]]`属性)。对于`T`的每个成员(和子成员)，颜色索引`m`必须是唯一的。
* 是一个结构类型，其成员满足列表中的约束。

#### 2.10.1.2 片段函数的显式图像块布局


例如：

```metal
struct I {
     float a [[raster_order_group(0)]];
     };

struct FragOut {
     float c [[color(0)]];
     I i [[imageblock_data]];
};

fragment FragOut
my_fragment(I i [[imageblock_data]])
{
    FragOut fragOut;
    ...
    return fragOut;
}
```


例如：

```metal
struct I {
     float a [[raster_order_group(0)]];
};

struct FragOut {
     float c [[color(0)]];
     I i [[imageblock_data]];
};

fragment FragOut
my_fragment(I i [[imageblock_data]],
            float c [[color(0)]])
{
    FragOut fragOut;
    ...
    return fragOut;
}

```

默认情况下，显式图像块存储和隐式图像块的存储分开。要在显式图像块和隐式图像块之间共享存储，请参加第4.7.3节。

### 2.10.2 内核函数的图像块

`imageblock<T>`类型(在头文件<metal_imageblocks>中定义)只能用于在内核函数或内核函数调用的用户函数中声明的参数。只有内核函数可以将参数声明为`imageblock<T>`类型。图像块中的数据仅对线程组中的线程可见。

内核函数的图像块参数声明为一下模板化类型：

```metal
class imageblock_layout_explicit;
class imageblock_layout_implicit;
template<typename T, typename L> struct imageblock;
```

有如下的限制：

- `L`是`imageblock_layout_explicit`或`imageblock_layout_implicit`之一
- `T`是一个结构
- `T`的每个成员可以是一下的任何一个：
    - 标量
    - 向量或打包向量
    - 像素数据类型
    - 一个数组，其元素是此列表中的一种类型
    - 一个结构，结构的成员是此列表中的类型之一

对于具有隐式布局的图像块(imageblock_layout_implicit)，结构的每个成员可以具有颜色附件（参见4.3.4节的表12中的`[[color(m)]]`属性）。对于`T`的每个成员（和子成员），颜色索引`m`必须是唯一的。

如果用户未指定图像块布局，则编译器基于`T`推断布局。如果`T`与隐式或显式图像块不兼容，则导致编译器错误。

显式和隐式图像块都可以作为参数传递给内核函数。这也使得在片段函数和内核函数之间共享显式和隐式图像块结构变得容易。默认情况下，显式图像块存储与隐式图像块的存储分开。要在显式图像块和隐式图像块之间共享存储，请参加第4.7.3节。

## 2.11 聚合类型

Metal支持多种聚合类型：数组(arrays)、结构(structs)、类(classes)和联合(unions)。

除非成员是指针类型，否则不要指定具有地址空间属性的结构成员。聚合类型的所有成员必须属于同一地址空间。（有关地址空间的详细信息，请参阅第4.2节。）

### 2.11.1 纹理、纹理缓冲区和采样器阵列

> iOS: 自v1.2支持纹理数组；自v2.0支持采样器阵列；自v2.1支持纹理缓冲区数组。
> macOS: 自v2.0支持纹理数组；自v2.0支持采样器阵列；自v2.1支持纹理缓冲区数组。

纹理数据可以声明为:

```metal
array<typename T, size_t N>
const array<typename T, size_t N>
```


#### 2.11.1.1 使用[]运算符访问数组元素

可以使用`[]`运算符访问纹理、纹理缓冲区和采样器数组的元素：

```metal
reference operator[] (size_t pos);
```

可以使用一下`[]`运算符的变体访问纹理、纹理缓冲区和采样器数组或模板化类型 `array_ref<T>`的元素：

```metal
constexpr const_reference  operator[] (size_t pos) const;
```

#### 2.11.1.2 数组容量

`size()`返回纹理、纹理缓冲区或采样器数组的元素个数。

```metal
constexpr size_t size();
constexpr size_t size() const;
```

例如

```metal
kernel void
my_kernel(const array<texture2d<float>, 10> src [[texture(0)]],
          texture2d<float, access::write> dst [[texture(10)]],
           ...)
{
    for (int i=0; i<src.size(); i++)
    {
        if (is_null_texture(src[i]))
            break;
        process_image(src[i], dst);
    }
}
```

#### 2.11.1.3 

```metal
constexpr array_ref();
constexpr array_ref(const array_ref &);
array_ref & operator=(const array_ref &);
constexpr array_ref(const T * array, size_t length);

template<size_t N>
constexpr array_ref(const T(&a)[N]);

template<typename T>
constexpr array_ref<T> make_array_ref(const T * array, size_t length)

template<typename T, size_t N>
constexpr array_ref<T> make_array_ref(const T(&a)[N])
```

## 2.12 参数缓冲区



## 2.13 Uniform类型

> 所有操作系统：自 v2.0 其支持uniform类型

### 2.13.1 Uniform类型的需求

在下面的函数示例中，变量`i`用于索引由`texInput`给出的纹理数组。变量`i`是不uniform的，即对于执行绘图或调度调用的图形或内核函数的线程，它可以具有不同的值，如下面的示例所示。因此，纹理采样硬件必须处理样本请求，该请求可以为执行绘图或调度的图形或内核函数的线程引用不同的纹理。

```metal
kernel void
my_kernel(array<texture2d<float>, 10> texInput,
           array<texture2d<float>, 10> texOutput,
           sampler s,
           ...,
           uint2 gid [[thread_position_in_grid]])
{
     int i = ...;
     float4 color = texInput[i].sample(s, float2(gid));
     ...;
     texOutput[i].write(color, float2(gid));
}
```

如果变量`i`对于执行绘制或调度调用的图形或内核函数的所有线程（即，统一）具有相同的值，并且如果该信息被传送到硬件，则纹理采样硬件可以应用适当的优化。可以为纹理写入进行类似的参数，其中在运行时计算的变量用作纹理数组的索引或索引到一个或多个缓冲区。

为了表明这个变量对于执行绘制或调度的图形或内核函数的所有线程是统一的，Metal着色语言添加了一个名为 `uniform`的新模板（在头文件`metal_uniform`中可用），可用于在图形或内核函数内部声明变量。此模板类智能使用算术类型（即布尔、整数和浮点类型）和向量类型进行实例化。

下面的代码是前一个示例的修改版本，其中变量`i`被声明为`uniform`类型。

```metal
kernel void
my_kernel(array<texture2d<float>, 10> texInput,
           array<texture2d<float>, 10> texOutput,
           sampler s,
           ...,
           uint2 gid [[thread_position_in_grid]])
{
     uniform<int> i = ...;
     float4 color = texInput[i].sample(s, float2(gid));
     ...;
     texOutput[i].write(color, float2(gid));
}
```

### 2.13.2 Uniform类型的行为

如果变量属于Uniform类型，并且变量对于内核或图形函数的所有线程没有相同的值，则行为未定义。

统一变量隐式转换为非统一类型。将使用统一变量计算的表达式的结果赋值给统一变量是合法的，但是将非统一的变量赋值给统一变量会导致编译时错误。在下面的示例中，乘法合法地将统一变量x转换为非统一乘积z。但是，将非统一变量z赋值给统一变量b会导致编译时错误。

```metal
uniform<int> x = ...;
int y = ...;
int z = x*y;        // x is converted to a non-uniform for a multiply
uniform<int> b = z; // illegal; compile-time error
```

声明一个uniform元素的数组：

```metal
uniform<float> bar[10]; // elements stored in bar array are uniform
```

`uniform`类型对于参数和函数的返回类型都是合法的。例如：

```metal
uniform<int> foo(...); // foo returns a uniform integer value
int bar(uniform<int> a, ...);
```

声明指向统一类型的指针是合法的，但声明统一指针是不合法的。例如：

```metal
device uniform<int> *ptr; // values pointed to by ptr are uniform 
uniform<device int *> ptr; // illegal; compile-time error
```

将 uniform变量与非uniform变量组合的表达式结构是非uniform。如果将非uniform的结果分配给uniform变量，如下例所示，则行为未定义。（前端可能会产生编译时错误，但不保证会这样做。）

```metal
uniform<int> i = ...;
int j = ...;
if (i < j) { // non-uniform result for expression (i < j)
    ...
    i++; //compile-timeerror,undefinedbehavior 
}
```

以下示例类似：

```metal
bool p = ... // non-uniform condition.
uniform<int> a = ..., b = ...;
uniform<int> c = p ? a : b; // compile-time error, undefined behavior
```

### 2.13.3 统一控制流程

当控制流条件测试基于统一数量时，所有程序实例在函数中的条件测试中遵循相同的路径。基于uniform数量的控制流程代码应该比基于非uniform数量的控制流程代码更有效。

## 2.14 类型转换和重新解释数据

`static_cast`运算符用于从标量或向量类型转换为另一标量或向量类型，没有饱和度并具有默认的舍入模式（即，当转换为浮点数时，舍入到最接近的偶数；当转换为整数时，舍入为零）。如果源类型是标量或向量布尔值，则将值`false`转换为零，将值`true`转换为1。

Metal添加了一个 `as_type<type-id>` 运算符，允许将任何标量或向量数据类型(不是指针)重新解释为另一个相同大小的标量或向量数据类型。操作输中的位直接返回不是作为新类型进行修改。不执行函数参数的常规类型提升。

例如，`as_type <float>（0x3f800000）`返回1.0f，如果将其视为IEEE-754单精度值，则为位模式`0x3f800000`的值。

使用 `as_type<type-id>`运算符将数据重新解释为具有不同字节数的类型会导致错误。

合法和非法类型转换的示例：

```metal
float f = 1.0f;
// Legal. Contains: 0x3f800000
uint u = as_type<uint>(f);

// Legal. Contains:
// (int4)(0x3f800000, 0x40000000, 0x40400000, 0x40800000)
float4 f = float4(1.0f, 2.0f, 3.0f, 4.0f);
int4 i = as_type<int4>(f);

int i;
// Legal.
short2 j = as_type<short2>(i);

half4 f;
// Error. Result and operand have different sizes
float4 g = as_type<float4>(f);

float4 f;
// Legal. g.xyz has same values as f.xyz.
// g.w is undefined
float3 g = as_type<float3>(f);
```

## 2.15 隐式类型转换

支持标量内置类型（void除外）之间的隐式转换。当完成隐式转换时，它不仅仅是对表达式值的重新解释，而是将该值转换为新类型中的等效值。例如，整数值被转换为浮点值5.0。

所有向量类型都被认为具有比标量类型更高的转换等级。不允许从向量类型到另一个向量或标量类型的隐式转换，并且会生成编译错误。例如，以下尝试将4分量整数向量转换为4分量浮点向量会失败。

```metal
int4 i;
float4 f = i;   // compile error.
```

支持从标量到向量类型的隐式转换。标量值在向量的每个元素复制。标量也可以用常用算术转换为向量或矩阵使用的元素类型。

例如：

```metal
float4 f = 2.0; // f = (2.0f, 2.0f, 2.0f, 2.0f)
```

不支持从标量到矩阵类型和向量到矩阵类型的隐式转换，并且会产生编译时错误。不允许从矩阵类型到另一个矩阵、向量或者标量类型的隐式转换，并且会产生编译时错误。

指针类型的隐式转换遵循C++ 14规范中描述的规则。